/*
 * Copyright 2021 Nikolai Kotchetkov.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *    http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk androidCompileSdkVersion
    buildToolsVersion = androidBuildToolsVersion

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=false"
        ]
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = commonLibVersions.compose_compiler_version
    }

    testOptions.unitTests.includeAndroidResources = true
    namespace 'com.motorro.rxlcemodel.composeview'

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':rx')
    implementation project(':viewmodel')

    // Bom
    implementation platform("androidx.compose:compose-bom:${commonLibVersions.compose_ui_bom}")

    // Core
    api "androidx.compose.ui:ui"
    // Tooling support (Previews, etc.)
    api "androidx.compose.ui:ui-tooling"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    api "androidx.compose.foundation:foundation"
    // Material Design
    api "androidx.compose.material:material"
    // Integration with ViewModels
    api "androidx.lifecycle:lifecycle-viewmodel-compose:${commonLibVersions.compose_viewmodel_version}"

    debugImplementation "androidx.compose.ui:ui-test-manifest"
    debugImplementation "androidx.compose.ui:ui-tooling:1.6.1"

    testImplementation "junit:junit:${commonLibVersions.junit}"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${commonLibVersions.mockito_kotlin}"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "androidx.compose.ui:ui-test-junit4:1.6.1"
    testImplementation 'org.robolectric:robolectric:4.11.1'
}

dokkaJavadoc.configure {
    outputDirectory.set(new File("$buildDir/javadoc"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

def libId = 'composeview'
def libName = 'composeview'
def libDesc = 'Base composables to use with LCE use-cases'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = rootProject.group
                artifactId = libId
                version = rootProject.version

                pom {
                    name = libName
                    description = libDesc
                    url = projectUrl
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = projectScm
                        developerConnection = projectScm
                        url = projectUrl
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
