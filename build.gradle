buildscript {
    ext.kotlin_version = '2.0.0'
    ext.dokka_version = '1.9.20'
    repositories {
        google()
        mavenCentral()
        maven {
            url="https://plugins.gradle.org/m2/"
        }
        maven {
            url="https://dl.bintray.com/kotlin/dokka"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.7.7'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.dokka' version "$dokka_version"
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/maven-publish-config.gradle'

setVersion(buildVersionName())
group = "com.motorro.rxlcemodel"
setDescription("A reactive data loading for Android based on RxJava and `Loading`/`Content`/`Error` states.")

ext {
    // Android
    androidBuildToolsVersion = '34.0.0'
    androidMinSdkVersion = 24
    androidTargetSdkVersion = 34
    androidCompileSdkVersion = 34
    versionName = project.version
    versionCode = buildVersionCode()

    //Bin-tray
    developerId = 'motorro'
    developerName = 'Nikolai Kotchetkov'
    developerEmail = 'motorro@gmail.com'

    bintrayUserOrg = "motorro"
    bintrayRepoName = "RxLceModel"

    projectScm = 'https://github.com/motorro/RxLceModel.git'
    projectUrl = 'https://github.com/motorro/RxLceModel'

    // Dokka
    docDir = "${projectDir}/docs"
}

allprojects {
    ext {
        commonLibVersions = [
                // Code
                "rx_java": '3.1.8',
                "coroutines": '1.8.1',
                "core_ktx_version": '1.13.1',
                "lifecycle_version": '2.8.1',
                "arch_version": "2.1.0",
                "nav_version": '2.7.7',
                "work_version": '2.9.0',

                // Tests
                "junit": '4.13.2',
                "mockito_kotlin": "2.2.0",
                "mockative": "1.2.6",

                // Compose
                "compose_ui_bom": '2024.05.00',
                "compose_ui_activity_version": "1.4.0",
                "compose_viewmodel_version": '2.8.1',
                "compose_livedata_version": "1.2.0",
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += [
                    "-opt-in=kotlin.RequiresOptIn",
                    "-Xinline-classes"
            ]
        }
    }

    // Test Logging
    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "skipped", "failed"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '  ', endItem = '  '
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }
}

tasks.register('runLceUnitTests') {
    dependsOn(':lce:check')
    group('verification')
    description 'Run unit tests for Lce library.'
}

tasks.register('runCacheUnitTests') {
    dependsOn(':cache:check')
    group('verification')
    description 'Run unit tests for Cache library.'
}

tasks.register('runRxUnitTests') {
    dependsOn(':rx:check')
    group('verification')
    description 'Run unit tests for Rx library.'
}

tasks.register('runCoroutinesUnitTests') {
    dependsOn(':coroutines:check')
    group('verification')
    description 'Run unit tests for Rx library.'
}

tasks.register('runDiskLruUnitTests') {
    dependsOn(':disklrucache:check')
    group('verification')
    description 'Run unit tests for DiskLruCache library.'
}

tasks.register('runKserializerUnitTests') {
    dependsOn(':kserializer:check')
    group('verification')
    description 'Run unit tests for KSerializer library.'
}

tasks.register('runViewmodelUnitTests') {
    dependsOn(':viewmodel:testDebugUnitTest')
    group('verification')
    description 'Run unit tests for ViewModel library.'
}

tasks.register('runComposeViewUnitTests') {
    dependsOn(':composeview:testDebugUnitTest')
    group('verification')
    description 'Run unit tests for Compose-view library.'
}

tasks.register('runSampleUnitTests') {
    dependsOn(':sample:check')
    group('verification')
    description 'Run unit tests for Sample application.'
}

tasks.register('runUnitTests') {
    dependsOn(
            'runLceUnitTests',
            'runCacheUnitTests',
            'runRxUnitTests',
            'runCoroutinesUnitTests',
            'runDiskLruUnitTests',
            'runKserializerUnitTests',
            'runViewmodelUnitTests',
            'runComposeViewUnitTests',
            'runSampleUnitTests'
    )
    group('verification')
    description 'Run unit tests for libraries.'
}

tasks.register('displayVersion') {
    description 'Display application version name'
    doLast {
        println("Project version: ${project.version}")
    }
}

dokkaGfmMultiModule.configure {
    outputDirectory = new File(docDir)
    includes.from("module.md")
}
dokkaHtmlMultiModule.configure {
    outputDirectory = new File(docDir)
    includes.from("module.md")
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

